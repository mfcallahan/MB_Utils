Include "MapBasic.Def"
Include "MbUtils.def"

'Wrapped .NET methods - these do not need to be included or declared outside this file
Declare Method _FolderBrowserDialog Class "MbUtils.FileUtils" Lib "MbUtils.dll" (ByVal dir As String, errStr As String) As String
Declare Method _DoesFileExist Class "MbUtils.FileUtils" Lib "MbUtils.dll" (ByVal fileN As String) As Logical
Declare Method _DoesFolderExist Class "MbUtils.FileUtils" Lib "MbUtils.dll" (ByVal dir As String) As Logical
Declare Method _ListFiles Class "MbUtils.FileUtils" Lib "MbUtils.dll" (ByVal dir As String, files() As String, ByVal numFiles As Integer, ByVal searchExt As String, errStr As String)
Declare Method _NumFiles Class "MbUtils.FileUtils" Lib "MbUtils.dll" (ByVal dir As String) As Integer
Declare Method _ListFolders Class "MbUtils.FileUtils" Lib "MbUtils.dll" (ByVal dir As String, folders() As String, ByVal numFolders As Integer, errStr As String)
Declare Method _NumFolders Class "MbUtils.FileUtils" Lib "MbUtils.dll" (ByVal dir As String) As Integer
Declare Method _KillTable Class "MbUtils.FileUtils" Lib "MbUtils.dll" (ByVal dir As String, errStr As String)
Declare Method _CreateFolder Class "MbUtils.FileUtils" Lib "MbUtils.dll" (ByVal dir As String, errStr As String)
Declare Method _DeleteFolder Class "MbUtils.FileUtils" Lib "MbUtils.dll" (ByVal dir As String, errStr As String)
Declare Method _DeleteFile Class "MbUtils.FileUtils" Lib "MbUtils.dll" (ByVal dir As String, errStr As String)

Declare Method _WriteToLogFile Class "MbUtils.Logger" Lib "MbUtils.dll" (ByVal logFilePath As String, ByVal logMsg As String, errStr As String)
Declare Method _DumpException Class "MbUtils.Logger" Lib "MbUtils.dll" (ByVal dir As String, ByVal ex As String)

Declare Method _Sleep Class "MbUtils.GenUtils" Lib "MbUtils.dll" (ByVal t As Integer, errStr As String)

'Win32 API functions - these do not need to be included or declared outside this file
Declare Function _GetUserNameWin Lib "advapi32.dll" Alias "GetUserNameA" (lpBuffer As String, nSize As Integer) As Integer
Declare Function _GetSystemMenu Lib "user32" Alias "GetSystemMenu" (ByVal hwnd As Integer, ByVal bRevert As Integer) As Integer
Declare Function _DrawMenuBar Lib "user32" Alias "DrawMenuBar" (ByVal hwnd As Integer) As Integer
Declare Function _DeleteMenu Lib "user32" Alias "DeleteMenu" (ByVal hMenu As Integer, ByVal nPosition As Integer, ByVal wFlags As Integer) As Integer
Declare Function _GetWindowLong Lib "user32" Alias "GetWindowLongA" (ByVal hwnd As Integer, ByVal nIndex As Integer) As Integer
Declare Function _SetWindowLong Lib "user32" Alias "SetWindowLongA" (ByVal hwnd As Integer, ByVal nIndex As Integer, ByVal dwNewLong As Integer) As Integer
Declare Function _MessageBox Lib "user32" Alias "MessageBoxA" (ByVal hwnd As Integer, ByVal lpText As String, ByVal lpCaption As String, ByVal wType As Integer) As Integer

'---------------------------------------------------------------------

Sub CreateFolder(ByVal fullPath As String)

Dim errStr As String
errStr = ""

Call _CreateFolder(fullPath, errStr)

If errStr <> "" Then
	Note "Error creating folder (MbUtils.mbo, MbUtils.dll): " + Chr$(13) + Chr$(13) + errStr
	Call _DumpException(ApplicationDirectory$(), errStr)
End If

End Sub

'---------------------------------------------------------------------

Sub DeleteFile(ByVal path As String)

Dim errStr As String
errStr = ""

Call _DeleteFile(path, errStr)

If errStr <> "" Then
	Note "Error deleting file (MbUtils.mbo, MbUtils.dll): " + Chr$(13) + Chr$(13) + errStr
	Call _DumpException(ApplicationDirectory$(), errStr)
End If

End Sub

'--------------------------------------------------------------------

Sub DeleteFolder(ByVal fullPath As String)

Dim errStr As String
errStr = ""

Call _DeleteFolder(fullPath, errStr)

If errStr <> "" Then
	Note "Error deleting folder (MbUtils.mbo, MbUtils.dll): " + Chr$(13) + Chr$(13) + errStr
	Call _DumpException(ApplicationDirectory$(), errStr)
End If

End Sub

'---------------------------------------------------------------------

Sub KillOpenTable(ByVal tableName As String)

If Not IsTableOpen(tableName) Then
	Note "Error deleting table " + tableName + " (MbUtils.mbo, MbUtils.dll): Table is not open"
	Exit Sub
End If

Close Table tableName

Dim errStr As String
errStr = ""

Call _KillTable(TableInfo(tableName, TAB_INFO_TABFILE), errStr)

If errStr <> "" Then
	Note "Error deleting files (MbUtils.mbo, MbUtils.dll): " + Chr$(13) + Chr$(13) + errStr
	Call _DumpException(ApplicationDirectory$(), errStr)
End If

End Sub

'---------------------------------------------------------------------

Sub ListFiles(ByVal fullPath As String, files() As String, ByVal searchExt As String)

Dim numFiles As Integer
numFiles = _NumFiles(fullPath)

ReDim files(numFiles)

Dim errStr As String
errStr = ""
Call _ListFiles(fullPath, files(), numFiles, searchExt, errStr)

If errStr <> "" Then
	Note "Error fetching files (MbUtils.mbo, MbUtils.dll): " + Chr$(13) + Chr$(13) + errStr
	Call _DumpException(ApplicationDirectory$(), errStr)
End If

End Sub

'---------------------------------------------------------------------

Sub ListFolders(ByVal fullPath As String, folders() As String)

Dim numFolders As Integer
numFolders = _NumFolders(fullPath)
ReDim folders(numFolders)

Dim errStr As String
errStr = ""

Call _ListFolders(fullPath, folders(), numFolders, errStr)

If errStr <> "" Then
	Note "Error fetching folders (MbUtils.mbo, MbUtils.dll): " + Chr$(13) + Chr$(13) + errStr
	Call _DumpException(ApplicationDirectory$(), errStr)
End If

End Sub

'---------------------------------------------------------------------

Sub ListOpenTables(openTables() As String)

ReDim openTables(NumTables())

Dim i As Integer
For i = 1 To NumTables()
	openTables(i) = TableInfo(i, TAB_INFO_NAME)
Next

End Sub

'---------------------------------------------------------------------

Sub PauseProgram(ByVal seconds As Integer)

Dim errStr As String
errStr = ""

Call _Sleep(seconds, errStr)

If errStr <> "" Then
	Note "Error pausing program (MbUtils.mbo, MbUtils.dll): " + Chr$(13) +Chr$(13) + errStr
	Call _DumpException(ApplicationDirectory$(), errStr)
End If

End Sub

'---------------------------------------------------------------------

Sub WindowRemoveCloseButton(ByVal winId As Integer)

Define MF_BYCOMMAND &H1024
Define GWL_STYLE -16

Dim hMenuHandle as Integer
Dim hWnd as Integer
Dim RC as Integer
Dim L as Integer

hWnd = WindowInfo(winId,WIN_INFO_WND)
hMenuHandle = _GetSystemMenu(hWnd, False)
If (hMenuHandle <> 0) Then

	'removes CLOSE for SysMenu & disables "X"
	RC = _DeleteMenu(hMenuHandle, SC_CLOSE, MF_BYCOMMAND)  
	'set the new style
	L = _SetWindowLong(hWnd, GWL_STYLE, L)
	'force a "redraw"
	RC = _DrawMenuBar(hWnd)

End If


End Sub

'---------------------------------------------------------------------

Sub WriteToLogFile(ByVal logFilePath As String, ByVal logMsg As String)

Dim errStr As String
errStr = ""
Call _WriteToLogFile(logFilePath, logMsg, errStr)

If errStr <> "" Then
	Note "Error writing to log file (MbUtils.mbo, MbUtils.dll): " + Chr$(13) + Chr$(13) + errStr
	Call _DumpException(ApplicationDirectory$(), errStr)
End If

End Sub

'---------------------------------------------------------------------

Function BrowseForFolder(ByVal startPath As String) As String

Dim errStr As String
Dim result As String
result = _FolderBrowserDialog(startPath, errStr)

If errStr <> "" Then
	Note "Error calling FolderBrowserDialog: (MbUtils.mbo, MbUtils.dll): " + Chr$(13) + Chr$(13) + errStr
	Call _DumpException(ApplicationDirectory$(), errStr)
	Exit Function
End If

BrowseForFolder = result

End Function

'---------------------------------------------------------------------

Function DoesFileExist(ByVal fullFilePath As String) As Logical

DoesFileExist = _DoesFileExist(fullFilePath)

End Function

'---------------------------------------------------------------------

Function DoesFolderExist(ByVal fullPath As String) As Logical

DoesFolderExist = _DoesFolderExist(fullPath)

End Function
 
'---------------------------------------------------------------------

Function GetUserName() As String

'Get user name
Dim userName As String
Dim nSize, nLeng As Integer

nSize = 255
userName = Space$(nSize)
nLeng = _GetUserNameWin(userName, nSize)

GetUserName = userName

End Function

'---------------------------------------------------------------------

Function IsTableOpen(ByVal tableName As String) As Logical

Dim result As Logical
result = False

If NumTables() = 0 Then	
	Goto return
End If

Dim i As Integer
For i = 1 to NumTables()
	If TableInfo(i, TAB_INFO_NAME) = tableName Then
		result = True
		Goto return
	End If
Next

return:
IsTableOpen = result

End Function

'---------------------------------------------------------------------

Function Note2(ByVal dialogTitle As String, ByVal messageText As String, iconButtons As Integer) As String

' hWnd handle of the Parent Window - should be MapInfo
' lptext message text
' lpCaption dialog title
' wType style flags
' RETURNS:
' 1 = OK
' 2 = CANCEL
' 3 = ABORT
' 4 = RETRY
' 5 = IGNORE
' 6 = YES
' 7 = NO
' NOTE: Set hWnd to 0 [zero]

Dim i as integer
i = _MessageBox(0, messageText, dialogTitle, iconButtons)

Do Case i
	Case 1
		Note2 = "ok"
	Case 2
		Note2 = "cancel"
	Case 3
		Note2 = "abort"
	Case 4
		Note2 = "retry"
	Case 5
		Note2 = "ignore"
	Case 6
		Note2 = "yes"
	Case 7
		Note2 = "no"
End Case

End Function

'---------------------------------------------------------------------

Function ReplaceInString(ByVal fullString As String, ByVal removeString As String, ByVal replacementString As String) As String

Dim removeStringPos As Integer
Dim startString As String
Dim endString As String

OnError Goto ErrorOccured

Search_For_Replace:
removeStringPos = InStr(1, LCase$(fullString),
LCase$(removeString))
If (removeStringPos > 0) Then
    If (removeStringPos = 1) Then
        fullString = replacementString + Mid$(fullString, Len(removeString)+1, Len(fullString) - Len(removeString))
    ElseIf ((removeStringPos + Len(removeString) - 1) = Len(fullString)) Then
        fullString = Left$(fullString, removeStringPos-1) + replacementString
	Else
		startString = Left$(fullString, removeStringPos-1)
		endString = Mid$(fullString, removeStringPos + Len(removeString), Len(fullString) - (removeStringPos + Len(removeString) - 1))
         fullString = startString + replacementString + endString
    End If

    Goto Search_For_Replace
End If

ReplaceInString = fullString

Exit Function

ErrorOccured:
ReplaceInString = ""

End Function

'---------------------------------------------------------------------